apply plugin: 'android'

dependencies {
    compile 'com.android.support:support-v4:19.0.1'
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.github.johnkil.android-appmsg:appmsg:1.2.0'
    compile project(':libs:HtmlTextView')
    compile project(':libs:spongycastle:core')
    compile project(':libs:spongycastle:pg')
    compile project(':libs:spongycastle:prov')
    compile project(':libs:AndroidBootstrap')
    compile project(':libs:StickyListHeaders:library')
    compile project(':OpenPGP-Keychain-API:libraries:keychain-api-library')
    compile project(':libs:zxing')
    compile project(':libs:zxing-android-integration')
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
    }

    if (project.hasProperty('signingStoreLocation') &&
        project.hasProperty('signingStorePassword') &&
        project.hasProperty('signingKeyAlias') &&
        project.hasProperty('signingKeyPassword')) {

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}
